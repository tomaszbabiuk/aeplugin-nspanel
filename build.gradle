buildscript {
    apply from: "../automate-everything/versions.gradle"

    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinSerializationPluginVersion"
    }
}

apply plugin: 'maven-publish'
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "org.jetbrains.kotlin.plugin.serialization"

group = 'eu.automateeverything'
version = '0.1.0'
ext.pluginsDir = '../automate-everything/output/plugins'

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions.jvmTarget = "$jdkVersion"
}

jar {
    manifest {
        attributes 'Plugin-Class': "eu.automateeverything.tabletsplugin.TabletsPlugin",
                'Plugin-Id': "tablets",
                'Plugin-Version': "0.1.0",
                'Plugin-Provider': "Tomasz Babiuk",
                'Plugin-Dependencies': ""
    }
}

dependencies {
    //projects
    shadow 'eu.automateeverything:data:0.1'
    shadow 'eu.automateeverything:devices:0.1'
    shadow 'eu.automateeverything:domain:0.1'
    shadow 'eu.automateeverything:conditions:0.1'

    //serialization
    shadow "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinSerializationVersion"
    shadow "org.jetbrains.kotlinx:kotlinx-serialization-cbor:$kotlinSerializationVersion"

    //pf4j
    shadow "org.pf4j:pf4j:$pf4jVersion"
    kapt "org.pf4j:pf4j:$pf4jVersion"
    annotationProcessor "org.pf4j:pf4j:$pf4jVersion"

    //californium
    implementation 'org.eclipse.californium:californium-core:3.8.0'


    //kotlin
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    shadow "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
}

task buildForAutomateEverything(type: Copy) {
    from shadowJar
    into pluginsDir
}

shadowJar {
    exclude 'kotlin/**'
    exclude 'org/intellij/**'
    exclude 'org/jetbrains/**'
    exclude 'org/slf4j/**'
    exclude 'org/pf4j/**'
    exclude 'com/google/gson/**'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                afterEvaluate {
                    from components.java
                }
            }
        }
        repositories {
            mavenLocal()
        }
    }
}